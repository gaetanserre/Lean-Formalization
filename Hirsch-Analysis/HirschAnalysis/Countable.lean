/-
 - Created in 2024 by GaÃ«tan SerrÃ©
 -/

/-
- https://github.com/gaetanserre/Hirsch-Analysis
-/


import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Data.Nat.Factorization.Defs
import Mathlib.NumberTheory.Padics.PadicVal.Basic

open Set Filter Topology Classical

set_option trace.Meta.Tactic.simp.rewrite true

set_option maxHeartbeats 1000000

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)

----------------------------------COUNTABLE-----------------------------------

/--
A set E is countable if it exists a surjection from â„• to E.
-/
def countable {u : Type} (E : Set u) := âˆƒ (f : â„• â†’ u), SurjOn f (univ) E

/--
â„• is countable.
-/
lemma N_countable : countable (univ : Set â„•) := by
  use (Î» n â†¦ n)
  intro n n_in
  use n

/--
â„¤ is countable
-/
lemma Z_countable : countable (univ : Set â„¤) := by
  let f := Î» (n : â„•) â†¦ if h : Even n then -((n / 2) : â„¤) else ((n - 1) / 2 + 1 : â„¤)
  have f_surj : SurjOn f univ univ := by {
    intro z _
    by_cases h : 0 < z
    Â· let n := 2 * (z - 1).toNat + 1
      have odd_n : Odd n := by use (z - 1).toNat
      use n
      refine âŸ¨trivial, ?_âŸ©
      rw [show f n = if h : Even n then -((n / 2) : â„¤) else ((n - 1) / 2 + 1 : â„¤) by rfl]
      split
      next h_if =>
        exfalso
        exact (Nat.odd_iff_not_even.mp odd_n) h_if
      have coe : (n : â„¤) = 2 * (z - 1) + 1 := by {
        have coe_tmp : ((z - 1).toNat : â„¤) = z - 1 := Int.toNat_of_nonneg (Int.le_sub_one_of_lt h)
        rw [â† coe_tmp]
        rfl

      }
      rw [coe]
      calc (2 * (z - 1) + 1 - 1) / 2 + 1 = (2 * (z - 1)) / 2 + 1 := by simp
      _ = (z - 1) + 1 := by simp
      _ = z := by simp
    push_neg at h
    let n := 2 * (-z).toNat
    have even_n : Even n := by {
      unfold Even
      use (-z).toNat
      ring
    }
    use n
    refine âŸ¨trivial, ?_âŸ©
    rw [show f n = if h : Even n then -((n / 2) : â„¤) else ((n - 1) / 2 + 1 : â„¤) by rfl]
    split
    Â· have coe : ((-z).toNat : â„¤) = -z := Int.toNat_of_nonneg (Int.neg_nonneg_of_nonpos h)
      let m_z := - z
      calc - ((n : â„¤) / 2) = -(2 * ((-z).toNat : â„¤) / 2) := by rw [show (n : â„¤) = ((2 * m_z.toNat) : â„¤) by rfl]
      _ = -(2 * m_z / 2) := by rw [coe]
      _ = -(m_z) := by simp
      _ = - (-z) := rfl
      _ = z := by simp
    contradiction
  }

  use f

/--
Let E a set. E is countable iff it exists a surjection from a countable set A to E.
-/
lemma countable_trans {u : Type} {E : Set u} : countable E â†” (âˆƒ (v : Type), âˆƒ (A : Set v), (countable A âˆ§ âˆƒ (f : v â†’ u), SurjOn f A E)) := by
  constructor
  Â· intro h_denomb
    use â„•, univ
    constructor
    Â· exact N_countable
    rcases h_denomb with âŸ¨f, f_surjâŸ©
    use f
  intro h
  rcases h with âŸ¨v, A, âŸ¨g, g_surjâŸ©, f, f_surjâŸ©
  have comp_surj : SurjOn (f âˆ˜ g) (univ) E := by {
    intro e e_in_E
    specialize f_surj e_in_E
    rcases f_surj with âŸ¨a, a_in_A, faâŸ©
    specialize g_surj a_in_A
    rcases g_surj with âŸ¨n, n_in_N, gnâŸ©
    use n
    constructor
    Â· assumption

    unfold Function.comp
    rwa [gn]
  }
  use (f âˆ˜ g)

/--
Let E be a countable set. Then any A âŠ† E is countable.
-/
lemma subset_of_countable_set {u : Type} {E : Set u} (A : Set u) (h : A âŠ† E) (he : countable E) : countable A := by
  let f := Î» (e : u) â†¦ e
  have f_surj : SurjOn f E A := by {
    intro a a_in_A
    use a, (h a_in_A)
  }
  exact countable_trans.mpr âŸ¨u, E, he, f, f_surjâŸ©

theorem prod_pow_primes_left {p q : â„•} [hp : Fact p.Prime] [hq : Fact q.Prime]
  (n m : â„•) (neq : p â‰  q) : padicValNat p (p^n * q^m) = n := by
  rw [padicValNat.mul (NeZero.ne' (p^n)).symm (NeZero.ne' (q^m)).symm]
  have padic_p_qm_eq_0 : padicValNat p (q ^ m) = 0 := by
    rw [padicValNat.pow _ (Nat.Prime.ne_zero hq.elim), padicValNat_primes neq, mul_zero]
  rw [padicValNat.prime_pow, padic_p_qm_eq_0, add_zero]

theorem prod_pow_primes_right {p q : â„•} [hp : Fact p.Prime] [hq : Fact q.Prime]
  (n m : â„•) (neq : q â‰  p) : padicValNat q (p^n * q^m) = m := by
  rw [padicValNat.mul (NeZero.ne' (p^n)).symm (NeZero.ne' (q^m)).symm]
  have padic_q_pn_eq_0 : padicValNat q (p ^ n) = 0 := by
    rw [padicValNat.pow _ (Nat.Prime.ne_zero hp.elim), padicValNat_primes neq, mul_zero]
  rw [padicValNat.prime_pow, padic_q_pn_eq_0, zero_add]

/--
â„•Â² is countable.
-/
lemma N2_countable : countable (univ : Set (â„• Ã— â„•)) := by
  let A := {n : â„• | âˆƒ (a b : â„•), n = 2^a * 3^b}
  let f := Î» n â†¦ if n âˆˆ A then (n.factorization 2, n.factorization 3) else (0, 0)
  have f_surj : SurjOn f A (univ : Set (â„• Ã— â„•)) := by {
    intro x _
    let n := 2^x.1 * 3^x.2
    use n
    have n_in_A : n âˆˆ A := âŸ¨x.1, x.2, rflâŸ©
    refine âŸ¨n_in_A, ?_âŸ©
    rw [show f n = if n âˆˆ A then (n.factorization 2, n.factorization 3) else (0, 0) by rfl]
    split
    Â· rw [show n.factorization 2 = padicValNat 2 n by rfl, show n.factorization 3 = padicValNat 3 n by rfl]
      rw [prod_pow_primes_left x.1 x.2 (show 2 â‰  3 by simp), prod_pow_primes_right x.1 x.2 (show 3 â‰  2 by simp)]
    contradiction
  }

  exact countable_trans.mpr âŸ¨â„•, A, subset_of_countable_set A (Î» _ _ â†¦ trivial) (N_countable), f, f_surjâŸ©

/--
We use a definition of a set being countable different to Mathlib's to match the book as much as possible. For completeness, we show that our definition is equivalent to Mathlib's. It corresponds to Corollary 1.5.
-/
lemma countable_iff {u : Type} {E : Set u} (hE : Set.Nonempty E) : countable E â†” Set.Countable E := by
  let s_t := {e : u // e âˆˆ E}
  constructor
  Â· intro h
    rcases h with âŸ¨f, f_surjâŸ©
    have surj' : âˆ€ e âˆˆ E, âˆƒ n, f n = e := by {
      intro e e_in_E
      specialize f_surj e_in_E
      rcases f_surj with âŸ¨n, _, f_nâŸ©
      use n
    }

    let g := Î» (e : s_t) â†¦ Nat.find (surj' e.val e.property)

    have g_inj : Function.Injective g := by {
      have f_g_id : âˆ€e (h : e âˆˆ E), f (g âŸ¨e, hâŸ©) = e := by {
        intro e he
        have h_g_e : g âŸ¨e, heâŸ© = Nat.find (surj' e he) := by rfl
        have tmp := Nat.find_spec (surj' e he)
        rwa [â†h_g_e] at tmp
      }
      intro e1 e2 hg
      ext
      rw [â†f_g_id e1.val e1.property, â†f_g_id e2.val e2.property, hg]
    }

    use g

  intro h
  rcases h with âŸ¨f, f_injâŸ©
  let fE := {n : â„• | âˆƒ (e : s_t), f e = n}
  have fE_countable : countable fE := subset_of_countable_set fE (Î» _ _ â†¦ trivial) N_countable

  have fE_dual_non_empty : âˆ€ n âˆˆ fE, Set.Nonempty {e | f e = n} := by {
    intro n hn
    rcases hn with âŸ¨e, fe_nâŸ©
    use e, fe_n
  }

  let Ï• := Î» n h â†¦ (Set.Nonempty.some (fE_dual_non_empty n h))
  let f' := Î» (n : â„•) â†¦ if h : n âˆˆ fE then (Ï• n h).val else (Set.Nonempty.some (hE))

  have f'_surj : SurjOn f' fE E := by {
    intro e e_in_E
    let e' : s_t := âŸ¨e, e_in_EâŸ©
    use (f e')
    have fe'_in_fE : f e' âˆˆ fE := by use e'
    refine âŸ¨fe'_in_fE, ?_âŸ©
    rw [show f' (f e') = if h : (f e') âˆˆ fE then (Ï• (f e') h).val else (Set.Nonempty.some (hE)) by rfl]
    split
    Â·
      have ff'e'_eq_fe' : f (Ï• (f e') fe'_in_fE) = (f e') :=
        Set.Nonempty.some_mem (fE_dual_non_empty (f e') fe'_in_fE)
      specialize f_inj ff'e'_eq_fe'
      have eq_subtype : âˆ€ {e1 e2 : s_t}, e1 = e2 â†’ e1.val = e2.val :=
        Î» {_} {_} a â†¦ congrArg Subtype.val a
      apply (eq_subtype f_inj)
    contradiction
  }

  exact countable_trans.mpr âŸ¨â„•, fE, fE_countable, f', f'_surjâŸ©

/--
The product of two countable sets is countable. (Proposition 1.6 p.2)
-/
lemma product_countable_set {u v : Type} {E : Set u} {A : Set v} (he : countable E) (ha : countable A) : countable {x : u Ã— v | x.1 âˆˆ E âˆ§ x.2 âˆˆ A} := by
  rcases he with âŸ¨fe, fe_surjâŸ©
  rcases ha with âŸ¨fa, fa_surjâŸ©
  let Ï• := Î» (x : â„• Ã— â„•) â†¦ (fe x.1, fa x.2)
  have Ï•_surj : SurjOn Ï• univ {x : u Ã— v | x.1 âˆˆ E âˆ§ x.2 âˆˆ A} := by {
    intro x âŸ¨hx1, hx2âŸ©
    rcases fe_surj hx1 with âŸ¨e, _, hfeâŸ©
    rcases fa_surj hx2 with âŸ¨a, _, hfaâŸ©
    use (e, a)
    refine âŸ¨trivial, ?_âŸ©
    rw [show Ï• (e, a) = (fe e, fa a) by rfl, hfe, hfa]
  }
  exact countable_trans.mpr âŸ¨â„•Ã—â„•, univ, N2_countable, Ï•, Ï•_surjâŸ©

/--
â„š is countable. (Example 1 p.3)
-/
lemma Q_countable : countable (univ : Set â„š) := by
  let A := {x : â„¤ Ã— â„• | 0 < x.2 âˆ§ x.1.natAbs.Coprime x.2}

  let f := Î» (x : â„¤ Ã— â„•) â†¦ if h : x âˆˆ A then ({num:=x.1, den:=x.2, den_nz:=Nat.not_eq_zero_of_lt h.1, reduced:=h.2} : â„š) else 0

  have f_surj : SurjOn f A univ := by {
    intro q _
    let x := (q.num, q.den)
    use x
    refine âŸ¨âŸ¨Rat.den_pos q, q.reducedâŸ©, ?_âŸ©
    rw [show f x = if h : x âˆˆ A then ({num:=x.1, den:=x.2, den_nz:=Nat.not_eq_zero_of_lt h.1, reduced:=h.2} : â„š) else 0 by rfl]
    split
    Â· rfl
    next h_if =>
      exfalso
      exact h_if âŸ¨Rat.den_pos q, q.reducedâŸ©
  }

  have countable_prod : countable (univ : Set (â„¤ Ã— â„•)) := by {
    have eq_set : {(x : â„¤ Ã— â„•) | x.1 âˆˆ univ âˆ§ x.2 âˆˆ univ} = (univ : Set (â„¤ Ã— â„•)) := by {
      ext x
      constructor
      Â· intro _
        trivial
      intro _
      exact âŸ¨trivial, trivialâŸ©
    }
    have univ_countable := product_countable_set Z_countable N_countable
    rwa [eq_set] at univ_countable
  }

  have countable_A := subset_of_countable_set A (show A âŠ† univ by exact Î» _ _ â†¦ trivial) countable_prod

  exact countable_trans.mpr âŸ¨â„¤Ã—â„•, A, countable_A, f, f_surjâŸ©

/--
Let A and E two sets such that E is uncountable. If it exists a surjection from A to E, then A is uncountable.
-/
lemma uncountable_trans {u v : Type} (A : Set u) {E : Set v} (h : Â¬countable E) : (âˆƒ (f : u â†’ v), SurjOn f A E) â†’ Â¬countable A := by
  intro exists_surj
  rcases exists_surj with âŸ¨f, f_surjâŸ©
  by_contra h_countable
  rcases h_countable with âŸ¨g, g_surjâŸ©
  have fg_surj_N_E : SurjOn (f âˆ˜ g) univ E := by {
    intro e e_in_E
    rcases f_surj e_in_E with âŸ¨a, a_in_A, faâŸ©
    rcases g_surj a_in_A with âŸ¨n, n_in_N, gnâŸ©
    use n, n_in_N
    rwa [show (f âˆ˜ g) n = f (g n) by rfl, gn]
  }
  have E_countable : countable E := by use (f âˆ˜ g)
  exact h E_countable

/--
Corrolary: if A âŠ† E is uncountable, then E is uncountable.
-/
lemma subset_of_uncountable_set {u : Type} {E A : Set u} (h : A âŠ† E) (uncountable : Â¬ countable A) : Â¬ countable E := by
  by_contra h_contr
  let f := Î» (e : u) â†¦ e
  have f_surj : SurjOn f E A := Î» e e_in_A â†¦ âŸ¨e, h e_in_A, rflâŸ©
  exact uncountable.elim (countable_trans.mpr âŸ¨u, E, h_contr, f, f_surjâŸ©)


------------------------------FORMALIZATION-----------------------------------
/--
The power set of â„• is uncountable. (Example 4 p.3)
-/
lemma powerset_N_uncountable : Â¬countable (ğ’« (univ : Set â„•)) :=
by

  by_contra h

  rcases h with âŸ¨Ï•, hâŸ©

  let A := {n : â„• | n âˆ‰ Ï• n}

  specialize h (Î» _ _ â†¦ trivial : A âˆˆ ğ’« (univ : Set â„•))
  rcases h with âŸ¨a, _, haâŸ©

  by_cases ain : a âˆ‰ A
  Â· have a_in_phi : Â¬ (a âˆ‰ Ï• a) := ain
    push_neg at a_in_phi
    rw [ha] at a_in_phi
    exact ain a_in_phi

  push_neg at ain
  have a_notin_phi_a : a âˆ‰ Ï• a := ain
  rw [ha] at a_notin_phi_a
  exact a_notin_phi_a ain

/-
- The set of all sequences in {0, 1}
-/
def zero_one := {u: â„• â†’ â„• | âˆ€n, u n = 0 âˆ¨ u n = 1}

/--
The set of all sequences in {0, 1} is uncountable. (Example 5 p.3)
-/
lemma zero_one_uncountable : Â¬countable zero_one :=
by
  let f := Î» (Ï• : â„• â†’ â„•) â†¦ {n : â„• | Ï• n = 1}

  have f_surj : SurjOn f zero_one (ğ’« (univ : Set â„•)) := by {
    intro A _
    let Ï• := indicator A (Î» _ â†¦ 1)
    use Ï•
    have Ï•_in_zero_one : Ï• âˆˆ zero_one := by {
      intro n
      by_cases h : n âˆˆ A
      Â· have ind_eq_1 : Ï• n = 1 := by {
          rw [indicator_apply_eq_self]
          exact Î» a â†¦ (a h).elim
        }
        apply Or.inr; assumption

      have ind_eq_0 : Ï• n = 0 := by {
          rw [indicator_apply_eq_zero]
          exact Î» a â†¦ (h a).elim
      }
      apply Or.inl; assumption
    }
    refine âŸ¨Ï•_in_zero_one, ?_âŸ©
    ext n
    constructor
    Â· intro n_in_f_Ï•
      have Ï•n_eq_1 : Ï• n = 1 := n_in_f_Ï•
      rw [indicator_apply_eq_self] at Ï•n_eq_1
      by_contra n_notin_A
      specialize Ï•n_eq_1 n_notin_A
      contradiction

    intro n_in_A
    have Ï•_eq_1 : Ï• n = 1 := by {
      rw [indicator_apply_eq_self]
      intro n_notin_A
      exact (n_notin_A n_in_A).elim
    }
    exact Ï•_eq_1
  }
  exact uncountable_trans zero_one powerset_N_uncountable âŸ¨f, f_surjâŸ©

/--
The set of all sequences to â„• is uncountable.
-/
example : Â¬ countable (univ : Set (â„• â†’ â„•)) :=
  subset_of_uncountable_set (Î» _ _ â†¦ trivial) zero_one_uncountable

/--
For any real number x and any positive number Îµ, it exists a rational q such that |x - q| < Îµ.
-/
lemma Q_dense (x : â„) : âˆ€ Îµ > 0, âˆƒ (q : â„š), |x - q| < Îµ :=
by
  intro Îµ Îµ_pos
  have h : x < x + Îµ := by linarith
  obtain âŸ¨q, hql, hqrâŸ© := exists_rat_btwn h
  use q
  have x_minus_q_neg : x - q < 0 := by linarith
  have abs_eq_neg : |x - q| = -(x - q) := by {
    rw [abs_eq_neg_self]
    linarith
  }
  rw [abs_eq_neg, show -(x - q) = q - x by ring]
  linarith

/--
For any real number x, it exists a sequence of rational that tends to x.
-/
example (x : â„) : âˆƒ (u : â„• â†’ â„š), Tendsto (Î» n â†¦ ((u n) : â„)) atTop (ğ“ x) :=
by
  let A := Î» (n : â„•) â†¦ {q : â„š | |x - q| < (1 / (n+1))}
  have non_empty : âˆ€ n,  Set.Nonempty (A n) := by {
    intro n
    obtain âŸ¨q, hqâŸ© : âˆƒ (q : â„š), |x - q| < (1 / (n+1)) := Q_dense x (1 / (n+1)) (Nat.one_div_pos_of_nat)
    use q, hq
  }

  let u := Î» (n : â„•) â†¦ Set.Nonempty.some (non_empty n)
  use u
  rw [Metric.tendsto_atTop']
  have dst : âˆ€ n, dist ((u n) : â„) x = |x - ((u n) : â„)| := Î» _ â†¦ Metric.mem_sphere'.mp rfl
  simp_rw [dst]
  intro Îµ Îµ_pos
  obtain âŸ¨N, hnâŸ© : âˆƒ (N : â„•), 1 / (N + 1) < Îµ := by {
    use (âŒˆ1 / ÎµâŒ‰â‚Š)
    have R_pos: 0 < (âŒˆ1 / ÎµâŒ‰â‚Š + 1 : â„) := Nat.cast_add_one_pos âŒˆ1 / ÎµâŒ‰â‚Š
    rw [show 1 / (âŒˆ1 / ÎµâŒ‰â‚Š + 1 : â„) = (âŒˆ1 / ÎµâŒ‰â‚Š + 1 : â„)â»Â¹ by exact one_div (âŒˆ1 / ÎµâŒ‰â‚Š + 1 : â„)]
    rw [inv_lt R_pos Îµ_pos, show Îµâ»Â¹ = 1 / Îµ by exact inv_eq_one_div Îµ]
    calc 1 / Îµ <= (âŒˆ1 / ÎµâŒ‰â‚Š : â„) := Nat.le_ceil (1 / Îµ)
    _ < (âŒˆ1 / ÎµâŒ‰â‚Š + 1 : â„) := lt_add_one (âŒˆ1 / ÎµâŒ‰â‚Š : â„)
  }
  use N
  intro n N_ltn
  calc |x - ((u n) : â„)| < 1 / (n + 1 : â„) := Nonempty.some_mem (non_empty n)
  _ < 1 / (N + 1 : â„) := Nat.one_div_lt_one_div N_ltn
  _ < Îµ := hn

/--
Let A be a family of disjoint, non-empty, open sets of â„. Then A is countable. (Example 9 p.4)
-/
example (A : NNReal â†’ Set â„) (h_nonempty : âˆ€ t, Set.Nonempty (A t)) (h_open : âˆ€ t, IsOpen (A t)) (h_disjoint : âˆ€ t1 t2, t1 â‰  t2 â†’ Disjoint (A t1) (A t2)) : countable {A t | t : NNReal} :=
by

  let q_A_t := Î» t â†¦ {q : â„š | (q : â„) âˆˆ A t}
  have nonempty_q_A_t : âˆ€ t, Set.Nonempty (q_A_t t) := by {
    intro t
    rcases (h_nonempty t) with âŸ¨x, hxâŸ©
    specialize h_open t
    rw [Metric.isOpen_iff] at (h_open)
    rcases h_open x hx with âŸ¨Îµ, hÎµ, ball_in_AtâŸ©
    obtain âŸ¨q, hql, hqrâŸ© := exists_rat_btwn (show x < x + Îµ by linarith)
    have q_dist : q - x < Îµ := by linarith
    have pos_abs : q - x = |q - x| := (abs_of_pos (show 0 < q - x by linarith)).symm
    rw [pos_abs, show |q - x| = dist (q : â„) x by rfl, â†Metric.mem_ball] at q_dist
    use q, (ball_in_At q_dist)
  }

  let q_A := Î» (q : â„š) â†¦ {x | âˆƒ t, A t = x âˆ§ (q : â„) âˆˆ A t}
  have nonempty_q_A : âˆ€ q, (âˆƒ t, q âˆˆ (q_A_t t)) â†’ Set.Nonempty (q_A q) := by {
    intro q h
    rcases h with âŸ¨t, hqâŸ©
    use (A t), t
    refine âŸ¨rfl, ?_âŸ©
    use hq
  }

  let Ï• := Î» q hq â†¦ Set.Nonempty.some (nonempty_q_A q hq)
  let f := Î» (q : â„š) â†¦ if h : âˆƒ t, q âˆˆ (q_A_t t) then Ï• q h else A 0

  have f_surj : SurjOn f univ {A t | t : NNReal} := by {
    intro At âŸ¨t, hâŸ©
    rw [â†h]
    rcases nonempty_q_A_t t with âŸ¨q, hqâŸ©
    use q
    refine âŸ¨trivial, ?_âŸ©
    rw [show f q = if h : âˆƒ t, q âˆˆ (q_A_t t) then Ï• q h else A 0 by rfl]
    split
    next h_if =>
      Â· have rfl_Ï• : Ï• q h_if âˆˆ q_A q := Nonempty.some_mem (nonempty_q_A q h_if)
        rcases rfl_Ï• with âŸ¨t2, h_eq_t2, hq2âŸ©
        rw [â†h_eq_t2]

        have At_eq_At2 : A t = A t2 := by {
          by_contra h_neg; push_neg at h_neg
          have t_neq : t â‰  t2 := Î» a â†¦ h_neg (congrArg A a)
          have disjoint : Disjoint (A t) (A t2) := h_disjoint t t2 t_neq
          have q_not_in : (q : â„) âˆ‰ A t2 := disjoint_left.mp disjoint hq
          exact q_not_in hq2
        }
        rw [At_eq_At2]

    next h_if =>
      push_neg at h_if
      specialize h_if t
      contradiction
  }

  exact countable_trans.mpr âŸ¨â„š, univ, Q_countable, f, f_surjâŸ©
