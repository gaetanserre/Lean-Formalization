/-
 - Created in 2024 by Ga√´tan Serr√©
-/

/-
- https://github.com/gaetanserre/Hirsch-Analysis
-/


import Mathlib.Init.Set
import Mathlib.Order.Filter.Basic
import Mathlib.Topology.Basic
import Mathlib.Tactic

open Set Filter Topology Classical

set_option trace.Meta.Tactic.simp.rewrite true

set_option maxHeartbeats 1000000

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y)

----------------------------------COUNTABLE-----------------------------------

/--
A set E is countable if it exists a surjection from ‚Ñï to E.
-/
def countable {u : Type} (E : Set u) := ‚àÉ (f : ‚Ñï ‚Üí u), SurjOn f (univ) E

/--
‚Ñï is countable.
-/
lemma N_countable : countable (univ : Set ‚Ñï) := by
  use (Œª n ‚Ü¶ n)
  intro n n_in
  use n

/--
‚Ñ§ is countable
-/
lemma Z_countable : countable (univ : Set ‚Ñ§) := by
  let f := Œª (n : ‚Ñï) ‚Ü¶ if h : Even n then -((n / 2) : ‚Ñ§) else ((n - 1) / 2 + 1 : ‚Ñ§)
  have f_surj : SurjOn f univ univ := by {
    intro z _
    by_cases h : 0 < z
    ¬∑ let n := 2 * (z - 1).toNat + 1
      have odd_n : Odd n := by use (z - 1).toNat
      use n
      refine ‚ü®trivial, ?_‚ü©
      rw [show f n = if h : Even n then -((n / 2) : ‚Ñ§) else ((n - 1) / 2 + 1 : ‚Ñ§) by rfl]
      split
      next h_if =>
        exfalso
        exact (Nat.odd_iff_not_even.mp odd_n) h_if
      have coe : (n : ‚Ñ§) = 2 * (z - 1) + 1 := by {
        have coe_tmp : ((z - 1).toNat : ‚Ñ§) = z - 1 := Int.toNat_of_nonneg (Int.le_sub_one_of_lt h)
        simpa [-Int.pred_toNat] using coe_tmp
      }
      rw [coe]
      calc (2 * (z - 1) + 1 - 1) / 2 + 1 = (2 * (z - 1)) / 2 + 1 := by simp
      _ = (z - 1) + 1 := by simp
      _ = z := by simp
    push_neg at h
    let n := 2 * (-z).toNat
    have even_n : Even n := by {
      unfold Even
      use (-z).toNat
      ring
    }
    use n
    refine ‚ü®trivial, ?_‚ü©
    rw [show f n = if h : Even n then -((n / 2) : ‚Ñ§) else ((n - 1) / 2 + 1 : ‚Ñ§) by rfl]
    split
    ¬∑ have coe : ((-z).toNat : ‚Ñ§) = -z := Int.toNat_of_nonneg (Int.neg_nonneg_of_nonpos h)
      let m_z := - z
      calc - ((n : ‚Ñ§) / 2) = -(2 * ((-z).toNat : ‚Ñ§) / 2) := by rw [show (n : ‚Ñ§) = ((2 * m_z.toNat) : ‚Ñ§) by rfl]
      _ = -(2 * m_z / 2) := by rw [coe]
      _ = -(m_z) := by simp
      _ = - (-z) := rfl
      _ = z := by simp
    contradiction
  }

  use f

/--
Let E a set. E is countable iff it exists a surjection from a countable set A to E.
-/
lemma countable_trans {u : Type} {E : Set u} : countable E ‚Üî (‚àÉ (v : Type), ‚àÉ (A : Set v), (countable A ‚àß ‚àÉ (f : v ‚Üí u), SurjOn f A E)) := by
  constructor
  ¬∑ intro h_denomb
    use ‚Ñï, univ
    constructor
    ¬∑ exact N_countable
    rcases h_denomb with ‚ü®f, f_surj‚ü©
    use f
  intro h
  rcases h with ‚ü®v, A, ‚ü®g, g_surj‚ü©, f, f_surj‚ü©
  have comp_surj : SurjOn (f ‚àò g) (univ) E := by {
    intro e e_in_E
    specialize f_surj e_in_E
    rcases f_surj with ‚ü®a, a_in_A, fa‚ü©
    specialize g_surj a_in_A
    rcases g_surj with ‚ü®n, n_in_N, gn‚ü©
    use n
    constructor
    ¬∑ assumption

    unfold Function.comp
    rwa [gn]
  }
  use (f ‚àò g)

/--
Let E be a countable set. Then any A ‚äÜ E is countable.
-/
lemma subset_of_countable_set {u : Type} {E : Set u} (A : Set u) (h : A ‚äÜ E) (he : countable E) : countable A := by
  let f := Œª (e : u) ‚Ü¶ e
  have f_surj : SurjOn f E A := by {
    intro a a_in_A
    use a, (h a_in_A)
  }
  exact countable_trans.mpr ‚ü®u, E, he, f, f_surj‚ü©

theorem prod_pow_primes_left {p q : ‚Ñï} [hp : Fact p.Prime] [hq : Fact q.Prime]
  (n m : ‚Ñï) (neq : p ‚â† q) : padicValNat p (p^n * q^m) = n := by
  rw [padicValNat.mul (NeZero.ne' (p^n)).symm (NeZero.ne' (q^m)).symm]
  have padic_p_qm_eq_0 : padicValNat p (q ^ m) = 0 := by
    rw [padicValNat.pow _ (Nat.Prime.ne_zero hq.elim), padicValNat_primes neq, mul_zero]
  rw [padicValNat.prime_pow, padic_p_qm_eq_0, add_zero]

theorem prod_pow_primes_right {p q : ‚Ñï} [hp : Fact p.Prime] [hq : Fact q.Prime]
  (n m : ‚Ñï) (neq : q ‚â† p) : padicValNat q (p^n * q^m) = m := by
  rw [padicValNat.mul (NeZero.ne' (p^n)).symm (NeZero.ne' (q^m)).symm]
  have padic_q_pn_eq_0 : padicValNat q (p ^ n) = 0 := by
    rw [padicValNat.pow _ (Nat.Prime.ne_zero hp.elim), padicValNat_primes neq, mul_zero]
  rw [padicValNat.prime_pow, padic_q_pn_eq_0, zero_add]

/--
‚Ñï¬≤ is countable.
-/
lemma N2_countable : countable (univ : Set (‚Ñï √ó ‚Ñï)) := by
  let A := {n : ‚Ñï | ‚àÉ (a b : ‚Ñï), n = 2^a * 3^b}
  let f := Œª n ‚Ü¶ if n ‚àà A then (n.factorization 2, n.factorization 3) else (0, 0)
  have f_surj : SurjOn f A (univ : Set (‚Ñï √ó ‚Ñï)) := by {
    intro x _
    let n := 2^x.1 * 3^x.2
    use n
    have n_in_A : n ‚àà A := ‚ü®x.1, x.2, rfl‚ü©
    refine ‚ü®n_in_A, ?_‚ü©
    rw [show f n = if n ‚àà A then (n.factorization 2, n.factorization 3) else (0, 0) by rfl]
    split
    ¬∑ rw [show n.factorization 2 = padicValNat 2 n by rfl, show n.factorization 3 = padicValNat 3 n by rfl]
      rw [prod_pow_primes_left x.1 x.2 (show 2 ‚â† 3 by simp), prod_pow_primes_right x.1 x.2 (show 3 ‚â† 2 by simp)]
    contradiction
  }

  exact countable_trans.mpr ‚ü®‚Ñï, A, subset_of_countable_set A (Œª _ _ ‚Ü¶ trivial) (N_countable), f, f_surj‚ü©

/--
We use a different definition of a set being countable to match the book as much as possible. For completeness, we show that our definition is equivalent to Mathlib's. It corresponds to Corollary 1.5.
-/
lemma countable_iff {u : Type} {E : Set u} [‚àÄ (e : u), Decidable (e ‚àà E)] (hE : Set.Nonempty E) : countable E ‚Üî Set.Countable E := by
  let s_t := {e : u // e ‚àà E}
  constructor
  ¬∑ intro h
    rcases h with ‚ü®f, f_surj‚ü©
    have surj' : ‚àÄ e ‚àà E, ‚àÉ n, f n = e := by {
      intro e e_in_E
      specialize f_surj e_in_E
      rcases f_surj with ‚ü®n, _, f_n‚ü©
      use n
    }

    let g := Œª (e : s_t) ‚Ü¶ Nat.find (surj' e.val e.property)

    have g_inj : Function.Injective g := by {
      have f_g_id : ‚àÄe (h : e ‚àà E), f (g ‚ü®e, h‚ü©) = e := by {
        intro e he
        have h_g_e : g ‚ü®e, he‚ü© = Nat.find (surj' e he) := by rfl
        have tmp := Nat.find_spec (surj' e he)
        rwa [‚Üêh_g_e] at tmp
      }
      intro e1 e2 hg
      ext
      rw [‚Üêf_g_id e1.val e1.property, ‚Üêf_g_id e2.val e2.property, hg]
    }

    use g

  intro h
  rcases h with ‚ü®f, f_inj‚ü©
  let fE := {n : ‚Ñï | ‚àÉ (e : s_t), f e = n}
  have fE_countable : countable fE := subset_of_countable_set fE (Œª _ _ ‚Ü¶ trivial) N_countable

  have fE_dual_non_empty : ‚àÄ n ‚àà fE, Set.Nonempty {e | f e = n} := by {
    intro n hn
    rcases hn with ‚ü®e, fe_n‚ü©
    use e, fe_n
  }

  let œï := Œª n h ‚Ü¶ (Set.Nonempty.some (fE_dual_non_empty n h))
  let f' := Œª (n : ‚Ñï) ‚Ü¶ if h : n ‚àà fE then (œï n h).val else (Set.Nonempty.some (hE))

  have f'_surj : SurjOn f' fE E := by {
    intro e e_in_E
    let e' : s_t := ‚ü®e, e_in_E‚ü©
    use (f e')
    have fe'_in_fE : f e' ‚àà fE := by use e'
    refine ‚ü®fe'_in_fE, ?_‚ü©
    rw [show f' (f e') = if h : (f e') ‚àà fE then (œï (f e') h).val else (Set.Nonempty.some (hE)) by rfl]
    split
    ¬∑
      have ff'e'_eq_fe' : f (œï (f e') fe'_in_fE) = (f e') :=
        Set.Nonempty.some_mem (fE_dual_non_empty (f e') fe'_in_fE)
      specialize f_inj ff'e'_eq_fe'
      have eq_subtype : ‚àÄ {e1 e2 : s_t}, e1 = e2 ‚Üí e1.val = e2.val :=
        Œª {_} {_} a ‚Ü¶ congrArg Subtype.val a
      apply (eq_subtype f_inj)
    contradiction
  }

  exact countable_trans.mpr ‚ü®‚Ñï, fE, fE_countable, f', f'_surj‚ü©

/--
The product of two countable sets is countable. (Proposition 1.6 p.2)
-/
lemma product_countable_set {u v : Type} {E : Set u} {A : Set v} (he : countable E) (ha : countable A) : countable {x : u √ó v | x.1 ‚àà E ‚àß x.2 ‚àà A} := by
  rcases he with ‚ü®fe, fe_surj‚ü©
  rcases ha with ‚ü®fa, fa_surj‚ü©
  let œï := Œª (x : ‚Ñï √ó ‚Ñï) ‚Ü¶ (fe x.1, fa x.2)
  have œï_surj : SurjOn œï univ {x : u √ó v | x.1 ‚àà E ‚àß x.2 ‚àà A} := by {
    intro x ‚ü®hx1, hx2‚ü©
    rcases fe_surj hx1 with ‚ü®e, _, hfe‚ü©
    rcases fa_surj hx2 with ‚ü®a, _, hfa‚ü©
    use (e, a)
    refine ‚ü®trivial, ?_‚ü©
    rw [show œï (e, a) = (fe e, fa a) by rfl, hfe, hfa]
  }
  exact countable_trans.mpr ‚ü®‚Ñï√ó‚Ñï, univ, N2_countable, œï, œï_surj‚ü©

/--
‚Ñö is countable. (Example 1 p.3)
-/
lemma Q_countable : countable (univ : Set ‚Ñö) := by
  let A := {x : ‚Ñ§ √ó ‚Ñï | 0 < x.2 ‚àß x.1.natAbs.Coprime x.2}

  let f := Œª (x : ‚Ñ§ √ó ‚Ñï) ‚Ü¶ if h : x ‚àà A then ({num:=x.1, den:=x.2, den_nz:=Nat.not_eq_zero_of_lt h.1, reduced:=h.2} : ‚Ñö) else 0

  have f_surj : SurjOn f A univ := by {
    intro q _
    let x := (q.num, q.den)
    use x
    refine ‚ü®‚ü®Rat.den_pos q, q.reduced‚ü©, ?_‚ü©
    rw [show f x = if h : x ‚àà A then ({num:=x.1, den:=x.2, den_nz:=Nat.not_eq_zero_of_lt h.1, reduced:=h.2} : ‚Ñö) else 0 by rfl]
    split
    ¬∑ rfl
    next h_if =>
      exfalso
      exact h_if ‚ü®Rat.den_pos q, q.reduced‚ü©
  }

  have countable_prod : countable (univ : Set (‚Ñ§ √ó ‚Ñï)) := by {
    have eq_set : {(x : ‚Ñ§ √ó ‚Ñï) | x.1 ‚àà univ ‚àß x.2 ‚àà univ} = (univ : Set (‚Ñ§ √ó ‚Ñï)) := by {
      ext x
      constructor
      ¬∑ intro _
        trivial
      intro _
      exact ‚ü®trivial, trivial‚ü©
    }
    have univ_countable := product_countable_set Z_countable N_countable
    rwa [eq_set] at univ_countable
  }


  have countable_A := subset_of_countable_set A (show A ‚äÜ univ by exact Œª _ _ ‚Ü¶ trivial) countable_prod

  exact countable_trans.mpr ‚ü®‚Ñ§√ó‚Ñï, A, countable_A, f, f_surj‚ü©

/--
Let A and E two sets such that E is uncountable. If it exists a surjection from A to E, then A is uncountable.
-/
lemma uncountable_trans {u v : Type} (A : Set u) {E : Set v} (h : ¬¨countable E) : (‚àÉ (f : u ‚Üí v), SurjOn f A E) ‚Üí ¬¨countable A := by
  intro exists_surj
  rcases exists_surj with ‚ü®f, f_surj‚ü©
  by_contra h_countable; push_neg at h_countable
  rcases h_countable with ‚ü®g, g_surj‚ü©
  have fg_surj_N_E : SurjOn (f ‚àò g) univ E := by {
    intro e e_in_E
    rcases f_surj e_in_E with ‚ü®a, a_in_A, fa‚ü©
    rcases g_surj a_in_A with ‚ü®n, n_in_N, gn‚ü©
    use n, n_in_N
    rwa [show (f ‚àò g) n = f (g n) by rfl, gn]
  }
  have E_countable : countable E := by use (f ‚àò g)
  exact h E_countable

/--
Corrolary: if A ‚äÜ E is uncountable, then E is uncountable.
-/
lemma subset_of_uncountable_set {u : Type} {E A : Set u} (h : A ‚äÜ E) (uncountable : ¬¨ countable A) : ¬¨ countable E := by
  by_contra h_contr
  let f := Œª (e : u) ‚Ü¶ e
  have f_surj : SurjOn f E A := Œª e e_in_A ‚Ü¶ ‚ü®e, h e_in_A, rfl‚ü©
  exact uncountable.elim (countable_trans.mpr ‚ü®u, E, h_contr, f, f_surj‚ü©)


------------------------------FORMALIZATION-----------------------------------

/--
For any real number x and any positive number Œµ, it exists a rational q such that |x - q| < Œµ.
-/
lemma Q_dense (x : ‚Ñù) : ‚àÄ Œµ > 0, ‚àÉ (q : ‚Ñö), |x - q| < Œµ :=
by
  intro Œµ Œµ_pos
  have h : x < x + Œµ := by linarith
  obtain ‚ü®q, hql, hqr‚ü© := exists_rat_btwn h
  use q
  have x_minus_q_neg : x - q < 0 := by linarith
  have abs_eq_neg : |x - q| = -(x - q) := by {
    rw [abs_eq_neg_self]
    linarith
  }
  rw [abs_eq_neg, show -(x - q) = q - x by ring]
  linarith

/--
For any real number x, it exists a sequence of rational that tends to x.
-/
example (x : ‚Ñù) : ‚àÉ (u : ‚Ñï ‚Üí ‚Ñö), Tendsto (Œª n ‚Ü¶ ((u n) : ‚Ñù)) atTop (ùìù x) :=
by
  let A := Œª (n : ‚Ñï) ‚Ü¶ {q : ‚Ñö | |x - q| < (1 / (n+1))}
  have non_empty : ‚àÄ n,  Set.Nonempty (A n) := by {
    intro n
    obtain ‚ü®q, hq‚ü© : ‚àÉ (q : ‚Ñö), |x - q| < (1 / (n+1)) := Q_dense x (1 / (n+1)) (Nat.one_div_pos_of_nat)
    use q, hq
  }

  let u := Œª (n : ‚Ñï) ‚Ü¶ Set.Nonempty.some (non_empty n)
  use u
  rw [Metric.tendsto_atTop']
  have dst : ‚àÄ n, dist ((u n) : ‚Ñù) x = |x - ((u n) : ‚Ñù)| := Œª _ ‚Ü¶ Metric.mem_sphere'.mp rfl
  simp_rw [dst]
  intro Œµ Œµ_pos
  obtain ‚ü®N, hn‚ü© : ‚àÉ (N : ‚Ñï), 1 / (N + 1) < Œµ := by {
    use (‚åà1 / Œµ‚åâ‚Çä)
    have R_pos: 0 < (‚åà1 / Œµ‚åâ‚Çä + 1 : ‚Ñù) := Nat.cast_add_one_pos ‚åà1 / Œµ‚åâ‚Çä
    rw [show 1 / (‚åà1 / Œµ‚åâ‚Çä + 1 : ‚Ñù) = (‚åà1 / Œµ‚åâ‚Çä + 1 : ‚Ñù)‚Åª¬π by exact one_div (‚åà1 / Œµ‚åâ‚Çä + 1 : ‚Ñù)]
    rw [inv_lt R_pos Œµ_pos, show Œµ‚Åª¬π = 1 / Œµ by exact inv_eq_one_div Œµ]
    calc 1 / Œµ <= (‚åà1 / Œµ‚åâ‚Çä : ‚Ñù) := Nat.le_ceil (1 / Œµ)
    _ < (‚åà1 / Œµ‚åâ‚Çä + 1 : ‚Ñù) := lt_add_one (‚åà1 / Œµ‚åâ‚Çä : ‚Ñù)
  }
  use N
  intro n N_ltn
  calc |x - ((u n) : ‚Ñù)| < 1 / (n + 1 : ‚Ñù) := Nonempty.some_mem (non_empty n)
  _ < 1 / (N + 1 : ‚Ñù) := Nat.one_div_lt_one_div N_ltn
  _ < Œµ := hn

/--
The power set of ‚Ñï is uncountable. (Example 4 p.3)
-/
lemma powerset_N_uncountable : ¬¨countable (ùí´ (univ : Set ‚Ñï)) :=
by

  by_contra h

  rcases h with ‚ü®œï, h‚ü©

  let A := {n : ‚Ñï | n ‚àâ œï n}

  specialize h (Œª _ _ ‚Ü¶ trivial : A ‚àà ùí´ (univ : Set ‚Ñï))
  rcases h with ‚ü®a, _, ha‚ü©

  by_cases ain : a ‚àâ A
  ¬∑ have a_in_phi : ¬¨ (a ‚àâ œï a) := ain
    push_neg at a_in_phi
    rw [ha] at a_in_phi
    exact ain a_in_phi

  push_neg at ain
  have a_notin_phi_a : a ‚àâ œï a := ain
  rw [ha] at a_notin_phi_a
  exact a_notin_phi_a ain

/-
- The set of all sequences in {0, 1}
-/
def zero_one := {u: ‚Ñï ‚Üí ‚Ñï | ‚àÄn, u n = 0 ‚à® u n = 1}

/--
The set of all sequences in {0, 1} is uncountable. (Example 5 p.3)
-/
lemma zero_one_uncountable : ¬¨countable zero_one :=
by
  let f := Œª (œï : ‚Ñï ‚Üí ‚Ñï) ‚Ü¶ {n : ‚Ñï | œï n = 1}

  have f_surj : SurjOn f zero_one (ùí´ (univ : Set ‚Ñï)) := by {
    intro A _
    let œï := indicator A (Œª _ ‚Ü¶ 1)
    use œï
    have œï_in_zero_one : œï ‚àà zero_one := by {
      intro n
      by_cases h : n ‚àà A
      ¬∑ have ind_eq_1 : œï n = 1 := by {
          rw [indicator_apply_eq_self]
          exact Œª a ‚Ü¶ (a h).elim
        }
        apply Or.inr; assumption

      have ind_eq_0 : œï n = 0 := by {
          rw [indicator_apply_eq_zero]
          exact Œª a ‚Ü¶ (h a).elim
      }
      apply Or.inl; assumption
    }
    refine ‚ü®œï_in_zero_one, ?_‚ü©
    ext n
    constructor
    ¬∑ intro n_in_f_œï
      have œïn_eq_1 : œï n = 1 := n_in_f_œï
      rw [indicator_apply_eq_self] at œïn_eq_1
      by_contra n_notin_A
      specialize œïn_eq_1 n_notin_A
      contradiction

    intro n_in_A
    have œï_eq_1 : œï n = 1 := by {
      rw [indicator_apply_eq_self]
      intro n_notin_A
      exact (n_notin_A n_in_A).elim
    }
    exact œï_eq_1
  }
  exact uncountable_trans zero_one powerset_N_uncountable ‚ü®f, f_surj‚ü©

example : ¬¨ countable (univ : Set (‚Ñï ‚Üí ‚Ñï)) :=
  subset_of_uncountable_set (Œª _ _ ‚Ü¶ trivial) zero_one_uncountable

/--
Let A be a family of disjoint, non-empty, open sets of ‚Ñù. Then A is countable. (Example 9 p.4)
-/
example (A : NNReal ‚Üí Set ‚Ñù) (h_nonempty : ‚àÄ t, Set.Nonempty (A t)) (h_open : ‚àÄ t, IsOpen (A t)) (h_disjoint : ‚àÄ t1 t2, t1 ‚â† t2 ‚Üí Disjoint (A t1) (A t2)) : countable {A t | t : NNReal} :=
by

  let q_A_t := Œª t ‚Ü¶ {q : ‚Ñö | (q : ‚Ñù) ‚àà A t}
  have nonempty_q_A_t : ‚àÄ t, Set.Nonempty (q_A_t t) := by {
    intro t
    rcases (h_nonempty t) with ‚ü®x, hx‚ü©
    specialize h_open t
    rw [Metric.isOpen_iff] at (h_open)
    rcases h_open x hx with ‚ü®Œµ, hŒµ, ball_in_At‚ü©
    obtain ‚ü®q, hql, hqr‚ü© := exists_rat_btwn (show x < x + Œµ by linarith)
    have q_dist : q - x < Œµ := by linarith
    have pos_abs : q - x = |q - x| := (abs_of_pos (show 0 < q - x by linarith)).symm
    rw [pos_abs, show |q - x| = dist (q : ‚Ñù) x by rfl, ‚ÜêMetric.mem_ball] at q_dist
    use q, (ball_in_At q_dist)
  }

  let q_A := Œª (q : ‚Ñö) ‚Ü¶ {x | ‚àÉ t, A t = x ‚àß (q : ‚Ñù) ‚àà A t}
  have nonempty_q_A : ‚àÄ q, (‚àÉ t, q ‚àà (q_A_t t)) ‚Üí Set.Nonempty (q_A q) := by {
    intro q h
    rcases h with ‚ü®t, hq‚ü©
    use (A t), t
    refine ‚ü®rfl, ?_‚ü©
    use hq
  }

  let œï := Œª q hq ‚Ü¶ Set.Nonempty.some (nonempty_q_A q hq)
  let f := Œª (q : ‚Ñö) ‚Ü¶ if h : ‚àÉ t, q ‚àà (q_A_t t) then œï q h else A 0

  have f_surj : SurjOn f univ {A t | t : NNReal} := by {
    intro At ‚ü®t, h‚ü©
    rw [‚Üêh]
    rcases nonempty_q_A_t t with ‚ü®q, hq‚ü©
    use q
    refine ‚ü®trivial, ?_‚ü©
    rw [show f q = if h : ‚àÉ t, q ‚àà (q_A_t t) then œï q h else A 0 by rfl]
    split
    next h_if =>
      ¬∑ have rfl_œï : œï q h_if ‚àà q_A q := Nonempty.some_mem (nonempty_q_A q h_if)
        rcases rfl_œï with ‚ü®t2, h_eq_t2, hq2‚ü©
        rw [‚Üêh_eq_t2]

        have At_eq_At2 : A t = A t2 := by {
          by_contra h_neg; push_neg at h_neg
          have t_neq : t ‚â† t2 := Œª a ‚Ü¶ h_neg (congrArg A a)
          have disjoint : Disjoint (A t) (A t2) := h_disjoint t t2 t_neq
          have q_not_in : (q : ‚Ñù) ‚àâ A t2 := disjoint_left.mp disjoint hq
          exact q_not_in hq2
        }
        rw [At_eq_At2]

    next h_if =>
      push_neg at h_if
      specialize h_if t
      contradiction
  }

  exact countable_trans.mpr ‚ü®‚Ñö, univ, Q_countable, f, f_surj‚ü©
